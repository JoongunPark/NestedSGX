############################################################################
# Copyright 2016 Intel Corporation
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
############################################################################
Import('*')
env.PartName('verifier')

api_headers = Pattern(src_dir='.',
                      includes=['api.h'],
                      excludes=[''],
                      recursive=False).files()

verifier_headers = Pattern(src_dir='.',
                           includes=['*.h'],
                           excludes=['api.h'],
                           recursive=False).files()


src_files = Pattern(src_dir='.',
                    includes=['*.c'],
                    excludes=['*-test.cc'],
                    recursive=True).files()


#unit tests
utest_files = Pattern(src_dir='.',
                      includes=['*-test.cc', '*-testhelper.cc'],
                      excludes=[],
                      recursive=True).files()
utest_include_files = Pattern(src_dir='.',
                              includes=['*-testhelper.h'],
                              excludes=[],
                              recursive=True).files()
utest_data_files = Pattern(src_dir='unittests/testdata/sigrl',
                           includes=['*.inc'],
                           excludes=[],
                           recursive=False).files()
parts_file = ['verifier.parts']

if 'install_package' in env['MODE']:
    env.InstallTopLevel(api_headers + verifier_headers,
                        sub_dir='epid/${PART_SHORT_NAME}')
    env.InstallTopLevel(src_files, sub_dir='epid/${PART_SHORT_NAME}')
    env.InstallTopLevel(utest_files + utest_include_files,
                        sub_dir='epid/${PART_SHORT_NAME}/unittests')
    env.InstallTopLevel(
        utest_data_files,
        sub_dir='epid/${PART_SHORT_NAME}/unittests/testdata/sigrl')
    env.InstallTopLevel(parts_file, sub_dir='epid/${PART_SHORT_NAME}')
else:
    env.DependsOn([
        Component('common'),
    ])

    env.Append(CPPPATH='#')

    testenv = env.Clone()
    outputs = env.Library('${PART_NAME}', src_files)
    env.Sdk(outputs)
    env.SdkInclude(api_headers, sub_dir='epid/${PART_SHORT_NAME}')

    if 'install_lib' in env['MODE']:
        env.InstallLib(outputs)
        env.InstallInclude(api_headers, sub_dir='${PART_SHORT_NAME}')

    testenv['UNIT_TEST_TARGET_NAME'] = "${PART_NAME}-${UNIT_TEST_TARGET}"
    testenv.UnitTest("utest",
                     utest_files,
                     command_args=[
                         '--gtest_color=yes',
                         '--gtest_print_time=1',
                         '--gtest_output=xml',
                         '--gtest_filter=**',
                     ],
                     make_pdb=(env.isConfigBasedOn('debug') or
                               env.isConfigBasedOn('static_crt_debug')),
                     depends=[Component('gtest'),
                              Component('common-testhelper'),
                              Component('verifier')],
                     INSTALL_BIN='${INSTALL_TEST_BIN}')
